prototype(Jonnitto.PrettyEmbedVideoStreaming:Content.Video) < prototype(Jonnitto.PrettyEmbedHelper:Content) {
    package = 'VideoStreaming'
    streaming = ${q(node).property('streaming')}

    title = ${q(node).filter('[instanceof Jonnitto.PrettyEmbedHelper:Mixin.Title]').property('title')}
    alternativeText = ${this.title}

    # Use this if you want to set a own Preview
    ownPreview = null

    # Should the video be opened on a lightbox?
    lightbox = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'lightbox'
    }

    # Whether the video plays inline on supported mobile devices.
    # Note that if set to false it does not imply that the video will always be played in fullscreen.
    playsinline = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'playsinline'
    }

    # If true, the browser will automatically seek back 
    # to the start upon reaching the end of the video.
    loop = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'loop'
    }

    # If true, the browser will offer controls to allow the user to control
    # video playback, including volume, seeking, and pause/resume playback.
    controls = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'controls'
    }

    # Whether the video is muted upon loading. Set automatically to true if autoplay is enabled
    muted = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'muted'
    }

    # If true, the video automatically begins to play back as soon
    # as it can do so without stopping to finish loading the data.
    autoplay = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'autoplay'
    }

    # This enumerated attribute is intended to provide a hint to the browser about what
    # the author thinks will lead to the best user experience with regards to what content
    # is loaded before the video is played. It may have one of the following values:
    #  - none       Indicates that the video should not be preloaded.
    #  - metadata   Indicates that only video metadata (e.g. length) is fetched.
    #  - auto       Indicates that the whole video file can be downloaded, even if the user is not expected to use it
    preload = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'preload'
    }

    # https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attr-crossorigin
    # anonymous || use-credentials
    crossorigin = Jonnitto.PrettyEmbedHelper:GetProperty {
        property = 'crossorigin'
    }

    width = null
    height = null

    renderer = Neos.Fusion:Component {
        @apply {
            props = ${props}
            overwrite = Neos.Fusion:DataStructure {
                poster = Neos.Neos:ImageUri {
                    @if.set = ${props.image}
                    asset = ${props.image}
                    async = true
                    maximumWidth = ${props.maximumWidth}
                }
            }
        }

        renderer = Neos.Fusion:Case {
            noVideoAndPoster {
                condition = ${!props.streaming && !props.poster}
                renderer = afx`<Jonnitto.PrettyEmbedHelper:Notification key='noVideoAndPoster' default='You need to define a video stream URL and a preview image.' />`
            }
            noVideo {
                condition = ${!props.streaming}
                renderer = afx`<Jonnitto.PrettyEmbedHelper:Notification key='noVideo' default='You need to define a video stream URL.' />`
            }
            noPoster {
                condition = ${!props.poster}
                renderer = afx`<Jonnitto.PrettyEmbedHelper:Notification key='noPoster' default='You need to define a preview image.' />`
            }
            default {
                condition = true
                renderer = afx`
                    <Jonnitto.PrettyEmbedVideoStreaming:Component.Video {...props} />
                `
            }
        }
    }
}
